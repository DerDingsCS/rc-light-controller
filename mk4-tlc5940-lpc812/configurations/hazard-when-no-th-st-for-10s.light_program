run always
    // 0: normal mode when car is driving
    // 1: hazard lights are on because steering and throttle have been in neutral for 10s
    var mode
    const MODE_NORMAL = 0
    const MODE_CAR_IS_IDLE = 1

    // Increments every 20 ms when st + th are in neutral.
    // Used for the 10s timer
    var count

    const LOOP_REPETITION_TIME = 20
    const IDLE_TIMEOUT = 500    // 10000 ms divided by 20 ms loop repetition time

    // Initialization
    mode = MODE_NORMAL
    count = 0

loop:
    // Loop timing is 20 ms
    sleep LOOP_REPETITION_TIME

    if mode == MODE_NORMAL
    goto normal_mode
    if mode == MODE_CAR_IS_IDLE
    goto idle_mode

    // This should never happen, just in case ...
    mode = MODE_NORMAL
    goto loop


// ------------------
normal_mode:
    // If we are not in neutral, reset the timeout counter
    if not neutral
    count = 0

    // If the steering is >20 or <-20 then reset the timeout counter
    if steering > 20
    count = 0
    if steering < -20
    count = 0

test_idle:
    // Increment the timeout counter (every 20ms because that is our sleep above!)
    count += 1

    // Has the 10 second timeout expired?
    if count < IDLE_TIMEOUT
    goto loop

    // Yes, the 10 seconds have expired! We switch to mode CAR_IS_IDLE and turn
    // the hazard lights on
    mode = MODE_CAR_IS_IDLE
    hazard = 1
    goto loop

// ------------------
idle_mode:
    // If we are not longer in neutral, switch back to normal mode
    if not neutral
    mode = MODE_NORMAL

    // If steering is > 20%, switch back to normal mode
    if steering > 20
    mode = MODE_NORMAL

    // If steering is < -20%, switch back to normal mode
    if steering < -20
    mode = MODE_NORMAL

    // If we set the mode to 0 above then we turn off the hazard light
    if mode == MODE_NORMAL
    hazard = 0

    goto loop

end