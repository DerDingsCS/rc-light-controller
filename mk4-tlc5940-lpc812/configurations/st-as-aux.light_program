// Light programs to re-use ST (steering) input as
// AUX function when automatic indicators are not
// required

// When using manual indicators, or when the car
// has no indicators at all, then the ST input
// of the light controller (or Pre-Processor)
// is not required.
//
// In this case the user may want to re-use the
// ST input to switch some other function. Below
// are two light programs demonstrate the user of
// ST for function other than indicators.
//
// Note: For this to work properly, it is advisable
// that in the Configurator, tab "Advanced settings",
// section "Servo inputs" the value for "Initial endpoint delta"
// is changed to 500 (default is 250).
// This ensures that ST expects a 1000..2000 us servo pulse instead
// of "auto-adjusting" after power on.


//-----------------------------------------
// Control OUT15 from a switch using ST input
//
// Use case: OUT15 turns on/off depeding on the
// switch position. This script assumes that the ST input
// of the light controller is connected to a channel
// that is controlled by two-position switch on the
// transmitter.
run always

// Declare the output to be switched on/off.
led l1 = led[15]
// If you want to add more outputs, just
// define additional ones here, for example:
// led l2 = led[12]
// led l3 = led[13]

loop:
    // Wait a bit so that the light controller
    // can handle other tasks
    sleep 20

    // The range of the steering input (and any other
    // input like TH, AUX, AUX2, AUX3) is -100..+100.
    // If the steering is less than -30 then we turn
    // the lights off.
    if steering < -30
    goto light_off

    // If the steering is greater than 30 then we turn
    // the lights on.
    if steering > 30
    goto light_on

    // In the range between -30 and +30 we do nothing,
    // which creates a hysteresis.
    goto loop

light_off:
    all leds = 0
    goto loop

light_on:
    all leds = 100
    goto loop

end


//-----------------------------------------
// Control the hazard lights (Warnblinker in German)
// from a switch using ST input
//
// Use case: The hazard lights turns on/off depeding on the
// switch position. This script assumes that the ST input
// of the light controller is connected to a channel
// that is controlled by two-position switch on the
// transmitter.
run always
loop:
    // Wait a bit so that the light controller
    // can handle other tasks
    sleep 20
    // If the steering is less than -30 then we turn
    // the hazard lights off.
    if steering < -30
    hazard = 0
    // If the steering is greater than 30 then we turn
    // the hazard lights on.
    if steering > 30
    hazard = 1
    // In the range between -30 and +30 we do nothing,
    // which creates a hysteresis.
    goto loop
end
