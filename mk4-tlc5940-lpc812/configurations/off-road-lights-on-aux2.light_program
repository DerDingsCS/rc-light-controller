// Light programs for to switch certain lights only in
// "off-road" mode.
//
// AUX2 is connected to a switch on the transmitter.
// The switch will turn certain lights on/off directly.
// When the switch is on, an off-road reversing light
// will also be enabled.
//
// Implementation:
// The easiest way to implement this function is
// with a reverse logic: we force the off-road lights
// off when AUX2 is off.
// When AUX2 is on, then we let the normal operation
// occur in the "LED configuration" table.
// In the "LED configuration" we configure the
// main off-road lights as "always on" and the
// off-road reversing lights as ordinary "reversing"
// lights.

run always
    const THRESHOLD_OFF = -33
    const THRESHOLD_ON = 33

    // Simple logic to turn the special variable
    // "program-state-0" off or on depending on
    // weather AUX2 is on or off.
    // THRESHOLD_OFF and THRESHOLD_ON implement
    // a hysteresis to make this script work well
    // even when AUX2 is connected to an analog control.
loop:
    // Check AUX2 every 20 ms
    sleep 20

    if aux2 < THRESHOLD_OFF
    program-state-0 = 1

    if aux2 > THRESHOLD_ON
    program-state-0 = 0

    goto loop
end

run when program-state-0
    // This script runs only when the special variable
    // "program-state-0" is 1. We set this variable to
    // 1 when AUX2 is off.
    //
    // When this script runs (AUX2 is off) then it "takes
    // over" the off-road LEDs and forces them to zero
    // brightness (LEDs off)
    led l1 = led[14] // off-road reversing light on OUT14
    led l2 = led[15] // off-road light on OUT15

    // If more LEDs should be controlled via the off-road
    // switch just add them as above, for example:
    //
    // led l3 = led[5]
    //
    // The code below does not need to be modified as it
    // uses the "all led" keyword, which means all LEDs
    // that are declared in this light program.

loop:
    all leds = 0
    sleep 20
    goto loop
end