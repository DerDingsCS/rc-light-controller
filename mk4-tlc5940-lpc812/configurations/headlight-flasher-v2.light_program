// Headlight flasher, revised version
//
// This light program flashes the headlights
// based on the state of AUX2 on the transmitter.
// It is assumed that AUX2 is a momentary push
// button on the transmitter, the assigned
// will be on as long as the button is pressed.
//
// The assigned LEDs are only "captured" by
// the light program when the headlights flasher
// is on, so they can still be used for their
// normal operation (e.g. high beam)

run always

const THRESHOLD_ON = 50
const THRESHOLD_OFF = 30

// This script constantly checks AUX2. If
// its value goes above the on-threshold then
// we set the special variable program-state-0.
// If the value is below the off-threshold we
// clear this special variable (hysteresis!)
loop:
    sleep 20
    if aux2 > THRESHOLD_ON
    program-state-0 = 1
    if aux2 < THRESHOLD_OFF
    program-state-0 = 0
    goto loop

end

// This light program runs only when the
// special variable program-state-0 has
// a value other than 0.
// This allows us to override LEDs only
// when AUX2 has been depressed.
run when program-state-0
    led l1 = led[4]
    led l2 = led[5]
    // If you want to add more LEDs, simply add them here:
    // led l3 = led[13]

loop:
    sleep 0
    all leds = 100
    goto loop

end
